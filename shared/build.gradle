plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    signingConfigs {
        release {
            storeFile file(System.properties.getOrDefault('RELEASE_STORE_FILE', '../debug.keystore'))
            storePassword System.properties.getOrDefault('RELEASE_STORE_PASSWORD', 'password')
            keyAlias System.properties.getOrDefault('RELEASE_KEY_ALIAS', 'alias_name')
            keyPassword System.properties.getOrDefault('RELEASE_KEY_PASSWORD', 'password')
        }
    }
    namespace 'com.jwoglom.controlx2.shared'
    compileSdk 36

    defaultConfig {
        minSdk 26
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // compose
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation "androidx.compose.ui:ui:$compose_version"

    // pumpx2-android dependencies
    implementation 'com.github.weliem:blessed-android:2.4.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "me.champeau.openbeans:openbeans:1.0.2"
    implementation "commons-codec:commons-codec:1.18.0"
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation "com.google.guava:guava:33.3.1-android"
    implementation 'org.bouncycastle:bcprov-jdk14:1.77'

    // pumpx2
    if (project.use_local_pumpx2) {
        implementation files("${project.m2_repository}/com/jwoglom/pumpx2/pumpx2-messages/${project.pumpx2_version}/pumpx2-messages-${project.pumpx2_version}.jar")
        implementation files("${project.m2_repository}/com/jwoglom/pumpx2/pumpx2-shared/${project.pumpx2_version}/pumpx2-shared-${project.pumpx2_version}.jar")
    } else {
        implementation "com.github.jwoglom.pumpX2:pumpx2-messages:v${project.pumpx2_version}"
        implementation "com.github.jwoglom.pumpX2:pumpx2-shared:v${project.pumpx2_version}"
    }

    testImplementation 'org.json:json:20230618'
}